
Dataplot Commands for generating control charts are
	XBAR CONTROL CHART Y X
	R CONTROL CHART Y X
	S CONTROL CHART Y X
	C CONTROL CHART Y X
	U CONTROL CHART Y X
	P CONTROL CHART Y X
	NP CONTROL CHART Y X
	CUSUM CONTROL CHART Y X
	EWMA CONTROL CHART Y X
	MOVING AVERAGE CONTROL CHART Y
	MOVING AVERAGE CONTROL CHART Y X
	MOVING RANGE CONTROL CHART Y
	MOVING RANGE CONTROL CHART Y X
	MOVING SD CONTROL CHART Y
	MOVING SD CONTROL CHART Y X
where Y is the response variable and X is the group identifier variable.
Dataplot computes the control limits. In some cases, you may have pre determined values to put in as control limits (e.g., based on historical data). Dataplot allows you to specify these limits by entering the following commands before the control chart command.
	LET TARGET = <value>
	LET LSL = <value>
	LET USL = <value>
		These allow you to specify the target, lower specification, and upper specification limit respectively.
		The appearance of the plot can be controlled by appropriate settings of the LINE and CHARACTER commands. Specifically, there are seven settings:
			the response curve
			the reference line at the Dataplot determined target value
			the reference line at the Dataplot determined upper specification limit
			the reference line at the Dataplot determined lower specification limit
			the reference line at the user-specified target value
			the reference line at the user-specified upper specification limit
			the reference line at the user-specified lower specification limit 
Return to the Control Chart Page

Dataplot Command for Confidence Interval for the Difference Between Two Proportions
The Dataplot command for a confidence interval for the difference of two proportions is
	DIFFERENCE OF PROPORTIONS CONFIDENCE INTERVAL Y1 Y2 
where Y1 contains the data for sample 1 and Y2 contains the data for sample 2. For large samples, Dataplot uses the binomial computation, not the normal approximation.
The following command sets the lower and upper bounds that define a success in the response variable
	ANOP LIMITS <lower bound> <upper bound> 
Return to the Difference of Two Proportions Page

Dataplot Command for Starting Values for Rational Function Models
Starting values for a rational function model can be obtained by fitting an exact rational function to a subset of the original data. The number of points in the subset should equal the number of parameters to be estimated in the rational function model. The EXACT RATIONAL FIT can be used to fit this subset model and thus to provide starting values for the rational function model. For example, to fit a quadratic/quadratic rational function model to data in X and Y, you might do something like the following.
	LET X2 = DATA 12 17 22 34 56
	LET Y2 = DATA 7 9 6 19 23
	EXACT 2/2 FIT Y2 X2 Y X
	FIT Y = (A0 + A1*X + A2*X**2)/(1 + B1*X + B2*X**2)
The DATA command is used to define the subset variables and EXACT 2/2 FIT is used to fit the exact rational function. The "2/2" identifies the degree of the numerator as 2 and the degree of the denominator as 2. It provides values for A0, A1, A2, B1, and B2, which are used to fit the rational function model for the full data set.
Hit the "Back" button on your browser to return to your original location.

Dataplot Command for Generalized ESD Test
The Dataplot command for the generalized ESD (Extreme Studentized Deviate) test is
	LET NOUTLIER = <value>
	EXTREME STUDENTIZED DEVIATE TEST Y
where Y is the response variable and NOUTLIER specifies the upper bound on the number of outliers to test.
Return to the Generalized ESD Page

Dataplot Command for Fitting
Dataplot can generate both linear and nonlinear fit commands.
For example, to generate a linear fit of Y versus X1, X2, and X3, the command is:
	FIT Y X1 X2 X3 
To generate quadratic and cubic fits of Y versus X, the commands are:
	QUADRATIC FIT Y X
	CUBIC FIT Y X
Nonlinear fits are generated by entering an equation. For example,
	FIT Y = A*(EXP(-B*X/10) - EXP(-X/10))
	FIT Y = C/(1+C*A*X**B)
	FIT Y = A - B*X - ATAN(C/(X-D))/3.14159
In the above equations, there are variables (X and Y), parameters (A, B, C, and D), and constants (10 and 3.14159). The FIT command estimates values for the parameters. If you have a parameter that you do not want estimated, enter it as a constant or with the "^" (e.g., FIT Y = ^C/(1+^C*A*X**B). The "^" substitutes the value of a parameter into a command.
You can also define a function and then fit the function. For example,
	LET FUNCTION F = C/(1+C*A*X**B)
	FIT Y = F
Hit the "Back" button on your browser to return to your original location.

Dataplot Commands for the PPCC Plot
The Dataplot command to generate a PPCC plot for unbinned data is:
	<dist> PPCC PLOT Y 
where <dist> identifies the distributional family and Y is the response variable.
The Dataplot command to generate a PPCC plot for binned data is:
	<dist> PPCC PLOT Y X 
where <dist> identifies the distributional family, Y is the counts variable, and X is the bin identifier variable.
Dataplot supports the PPCC plot for over 25 distributions. Some of the most common are WEIBULL, TUKEY LAMBDA, GAMMA, PARETO, and INVERSE GAUSSIAN. Enter the command LIST DISTRIBUTIONS for a list of supported distributions.
Dataplot allows you to specify the range of the shape parameter. Dataplot generates 50 probability plots in equally spaced intervals from the smallest value of the shape parameter to the largest value of the shape parameter. For example, to generate a Weibull PPCC plot for values of the shape parameter gamma from 2 to 4, enter the commands:
	LET GAMMA1 = 2
	LET GAMMA2 = 4
	WEIBULL PPCC PLOT Y
The command LIST DISTRIBUTIONS gives the name of the shape parameter for the supported distributions. The "1" and "2" suffixes imply the minimum and maximum value for the shape parameter, respectively.
Whenever Dataplot generates a PPCC plot, it saves the following internal parameters:
	MAXPPCC - the maximum correlation coefficient from the PPCC plot.
	SHAPE - the value of the shape parameter that generated the maximum correlation coefficient. 
Return to the PPCC Plot Page

Dataplot Command for Proportion Defective Confidence Interval
The Dataplot command for a confidence interval for the proportion defective is
	PROPORTION CONFIDENCE LIMITS Y 
where Y is a response variable. Note that for large samples, Dataplot generates the interval based on the exact binomial probability, not the normal approximation.
The following command sets the lower and upper bounds that define a success in the response variable:
	ANOP LIMITS <lower bound> <upper bound> 
Return to the Proportion Defective Page

